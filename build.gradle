description = 'Kotlin-ext'

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

if (rootProject != project) {
    configure(rootProject) {
        apply plugin: 'idea'
    }
}

subprojects {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'kotlin'
    apply plugin: 'pride'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    javadocJar.dependsOn javadoc

    build.dependsOn sourceJar, javadocJar

    dependencies {
        compile "org.slf4j:slf4j-api:${version_slf4j}"

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testRuntime "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
        testCompile ("org.jetbrains.spek:spek:$spekVersion") {
            exclude group: "org.jetbrains.kotlin"
            exclude group: "junit"
        }
        testCompile ("junit:junit:$junitVersion") {
            exclude group: "org.hamcrest"
        }
        testCompile "ch.qos.logback:logback-classic:${version_logback}"
        testCompile "ch.qos.logback:logback-access:${version_logback}"
        testRuntime "org.slf4j:jcl-over-slf4j:${version_slf4j}"
        testRuntime "org.slf4j:jul-to-slf4j:${version_slf4j}"
        testRuntime "org.slf4j:log4j-over-slf4j:${version_slf4j}"
    }

    repositories {
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }

    configurations.all {
        exclude group: "org.slf4j", module: "slf4j-simple"
        exclude group: "commons-logging"
        exclude group: "log4j"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.slf4j", module: "slf4j-log4j12"

        resolutionStrategy {
            cacheDynamicVersionsFor 1, 'days'  // things with wildcard version numbers
            cacheChangingModulesFor 4, 'hours'  // things with same version, but content of module changes, i.e. SNAPSHOT
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            maven(MavenPublication) {
                group = "com.collokia.kotlinx"
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
        repositories {
            maven {
                url "https://collokia.artifactoryonline.com/collokia/collokia-oss"
                credentials {
                    username "${collokiaRepoUser}"
                    password "${collokiaRepoPass}"
                }
            }
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.springframework.build.gradle:propdeps-plugin:$propdepsVersion"
        classpath "com.prezi.gradle.pride:gradle-pride-plugin:$prideVersion"
    }
}

